#version 460 core
#extension GL_EXT_ray_tracing : enable


#define PI 3.1415926535897932384626433832795

float epsilon = 0.001f;
float ray_max = 1000.0f;

uint max_bounces = 6;

// taken from https://www.shadertoy.com/view/tlVczh
void basis(in vec3 n, out vec3 f, out vec3 r)
{
   //looks good but has this ugly branch
  if(n.z < -0.99995)
    {
        f = vec3(0.0 , -1.0, 0.0);
        r = vec3(-1.0, 0.0, 0.0);
    }
    else
    {
    	float a = 1.0/(1.0 + n.z);
    	float b = -n.x*n.y*a;
    	f = vec3(1.0 - n.x*n.x*a, b, -n.x);
    	r = vec3(b, 1.0 - n.y*n.y*a , -n.y);
    }

    f = normalize(f);
    r = normalize(r);
}

// https://stackoverflow.com/questions/596216/formula-to-determine-perceived-brightness-of-rgb-color
float luminance(vec3 color) {
    return (0.299*color.r + 0.587*color.g + 0.114*color.b);
}
#line 4

// taken from https://stackoverflow.com/questions/4200224/random-noise-functions-for-glsl
#define PI 3.1415926535897932384626433832795

// A single iteration of Bob Jenkins' One-At-A-Time hashing algorithm.
uint hash( uint x ) {
    x += ( x << 10u );
    x ^= ( x >>  6u );
    x += ( x <<  3u );
    x ^= ( x >> 11u );
    x += ( x << 15u );
    return x;
}


// Construct a float with half-open range [0:1] using low 23 bits.
// All zeroes yields 0.0, all ones yields the next smallest representable value below 1.0.
float floatConstruct( uint m ) {
    const uint ieeeMantissa = 0x007FFFFFu; // binary32 mantissa bitmask
    const uint ieeeOne      = 0x3F800000u; // 1.0 in IEEE binary32

    m &= ieeeMantissa;                     // Keep only mantissa bits (fractional part)
    m |= ieeeOne;                          // Add fractional part to 1.0

    float  f = uintBitsToFloat( m );       // Range [1:2]
    return f - 1.0;                        // Range [0:1]
}



// Pseudo-random value in half-open range [0:1].
float random( float x ) { return floatConstruct(hash(floatBitsToUint(x))); }


float seed_random( inout uint rnd ) { rnd = hash(rnd); return floatConstruct(rnd); }
#line 5

struct RayPayload
{
    // ray data
    uint depth;
    uint seed;
    bool hit;
    
    // hit data
    uint instance;
    vec3 position;
    vec3 normal;
    vec2 uv;
};


struct MaterialPayload
{
    // ray data
    uint seed;

    // input data
    uint instance;
    vec3 position;
    vec3 normal;
    vec2 uv;
    vec3 direction;

    // output data
    vec3 emission;
    vec3 surface_color;
    vec3 sample_direction;
    float sample_pdf;
};
#line 6

layout(set = 1, binding = 8) uniform sampler2D tex[16];
layout(set = 1, binding = 9) readonly buffer TextureIndexData {uint data[];} texture_indices;

vec3 sample_texture(uint instance, vec2 uv) {
    return texture(tex[texture_indices.data[instance]], uv).rgb;
}
#line 7

vec3 sample_cosine_hemisphere(float u1, float u2)
{
    float theta = 0.5 * acos(-2.0 * u1 + 1.0);
    float phi = u2 * 2.0 * PI;

    float x = cos(phi) * sin(theta);
    float y = sin(phi) * sin(theta);
    float z = cos(theta);

    return vec3(x, y, z);
}

// taken from https://www.cim.mcgill.ca/~derek/ecse689_a3.html
vec3 sample_phong_lobe(float u1, float u2, float n)
{
    float alpha = sqrt(1.0 - pow(u1, 2.0 / (n+1.0)));

    float x = alpha * cos(2.0 * PI * u2);
    float y = alpha * sin(2.0 * PI * u2);
    float z = pow(u1, 1.0 / (n+1.0));

    return vec3(x, y, z);
}
#line 8

float fresnel_factor(float eta, float cos_i, float cos_t) {
    float r_s = (eta * cos_i - cos_t) / (eta * cos_i + cos_t);
    float r_p = (cos_i - eta * cos_t) / (cos_i + eta * cos_t);
    return clamp((r_s * r_s + r_p * r_p) * 0.5, 0, 1);
}

float snell(float eta, float cos_i) {
    return 1.0 - (1.0 - cos_i * cos_i) * eta * eta;
}

struct FresnelTerm {
    float cos_t;
    float factor;
};

FresnelTerm fresnel(float eta, float cos_i) {
    float eta2 = eta;
    if (cos_i < 0) eta2 = 1.0 / eta;
    float cos2_t = snell(eta2, cos_i);
    FresnelTerm result;
    if (cos2_t <= 0.0) {
        result.cos_t = 0.0;
        result.factor = 1.0;
    } else {
        float cos_t = sqrt(cos2_t);
        float factor = fresnel_factor(eta2, abs(cos_i), cos_t);
        if (cos_i < 0) cos_t = -cos_t;
        result.cos_t = cos_t;
        result.factor = factor;
    }
    return result;
}
#line 9

layout(set = 0, binding = 3) readonly buffer FloatParams {float data[];} float_params;
layout(set = 0, binding = 4) readonly buffer Vec3Params {vec4 data[];} vec3_params;

layout(location = 0) callableDataInEXT MaterialPayload material_payload;


void main() {
    float eta = float_params.data[0];
    float cos_o = dot(material_payload.direction, material_payload.normal);
    FresnelTerm f = fresnel(eta, cos_o);

    if (seed_random(material_payload.seed) <= f.factor) {
        // reflection
        vec3 reflection = reflect(material_payload.direction, material_payload.normal);
        material_payload.sample_direction = reflection;
    } else {
        // refraction
        vec3 refraction;
        if (dot(material_payload.direction, material_payload.normal) < 0) {
            refraction = refract(material_payload.direction, material_payload.normal, eta);
        } else {
            refraction = refract(material_payload.direction, -material_payload.normal, 1.0 / eta);
        }
        material_payload.sample_direction = refraction;
    }

    material_payload.surface_color = vec3(1.0);
    material_payload.sample_pdf = 1.0;
}
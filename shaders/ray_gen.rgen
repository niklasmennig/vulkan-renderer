#version 460 core
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_ray_flags_primitive_culling : enable
#extension GL_GOOGLE_include_directive : enable

#include "common.glsl"
#include "camera.glsl"


layout(push_constant) uniform PushConstants {
    float time;
    uint clear_accumulated;
} push_constants;

#include "random.glsl"
#include "payload.glsl"
layout(location = 0) rayPayloadEXT RayPayload payload;

layout(set = 0, binding = 0) uniform accelerationStructureEXT as;
layout(set = 0, binding = 1) uniform RayGenerationData {
    CameraData camera;
} raygen_data;
layout(set = 0, binding = 2, rgba32f) uniform image2D images[];


vec3 compute_ray_direction(vec2 ndc) {
    vec3 right = raygen_data.camera.right.xyz;
    vec3 up = raygen_data.camera.up.xyz;
    vec3 forward = raygen_data.camera.forward.xyz;

    float fov_x_half = radians(raygen_data.camera.fov_x) / 2.0;

    float aspect_ratio = float(gl_LaunchSizeEXT.y) / float(gl_LaunchSizeEXT.x);
    float fov_y_half = fov_x_half * aspect_ratio;

    vec3 dir = normalize(forward) + ndc.x * abs(tan(fov_x_half)) * normalize(right) + ndc.y * abs(tan(fov_y_half)) * normalize(up);
    return normalize(dir);
}


void main() {
    int frame_samples = 1;
    vec3 sample_sum = vec3(0);
    for (int s = 0; s < frame_samples; s++) {
        uint seed = gl_LaunchIDEXT.x * 313 + gl_LaunchIDEXT.y * 12 + uint(push_constants.time) * 543 + push_constants.clear_accumulated * 78 + s * 234;

        vec2 pixel_center = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
        vec2 pixel_offset = vec2(seed_random(seed), seed_random(seed)) - vec2(0.5);

        vec2 ndc = ((pixel_center + pixel_offset) / vec2(gl_LaunchSizeEXT.xy)) * 2 - 1;
        ndc.y *= -1;

        // initialize payload
        vec3 ray_origin = raygen_data.camera.origin.xyz;
        vec3 ray_direction = compute_ray_direction(ndc);

        payload.color = vec3(0);
        payload.contribution = vec3(1);
        payload.depth = 0;
        payload.seed = seed;

        traceRayEXT(
                    as,
                    gl_RayFlagsOpaqueEXT,
                    0xff,
                    0,
                    0,
                    0,
                    ray_origin,
                    epsilon,
                    ray_direction,
                    ray_max,
                    0
                );
        
        sample_sum += payload.color;
    }

    ivec2 img_coord = ivec2(gl_LaunchIDEXT.xy);
    uint sample_count = push_constants.clear_accumulated;
    vec3 color = clamp(sample_sum / float(frame_samples), vec3(0,0,0), vec3(10,10,10));
    if (sample_count <= 0) {
        imageStore(images[0], img_coord, vec4(color, 1.0));
    } else {
        vec3 prev_color = imageLoad(images[0], img_coord).rgb;
        vec3 new_color = (prev_color * (sample_count) + color) / (sample_count + 1);
        imageStore(images[0], img_coord, vec4(new_color, 1.0));
    }

    // instance indices AOV
    uint i = payload.primary_hit_instance;
    vec3 instance_color =  vec3(floor(i/(255*255))/255.0, floor(i/255)/255.0, (mod(i,255))/255.0);
    if (payload.primary_hit_instance == NULL_INSTANCE) instance_color = vec3(1.0);
    imageStore(images[1], img_coord, vec4(instance_color, 1.0));

    // albedo AOV
    imageStore(images[2], img_coord, vec4(payload.primary_hit_albedo, 1.0));
}